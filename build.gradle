// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.2.1' apply false
    id 'com.android.library' version '7.2.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.6.21' apply false
    id 'jacoco'
}

allprojects {
    apply plugin: 'jacoco'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

task jacocoFullReport(type: JacocoReport, group: 'Coverage reports') {
    group = 'Verification'
    description = 'Generate Jacoco aggregate report for all modules'

    def projects = subprojects.findAll { it.getTasksByName("jacocoTestReport", false) }

    //noinspection GrUnresolvedAccess
    dependsOn(projects.jacocoTestReport)

    final source = files(projects.jacocoTestReport.sourceDirectories)

    additionalSourceDirs.setFrom source
    sourceDirectories.setFrom source

    classDirectories.setFrom files(projects.jacocoTestReport.classDirectories)
    executionData.setFrom files(projects.jacocoTestReport.executionData)

    reports {
        html.enabled(true)
        csv.enabled(true)
    }
}

task openJacocoFullReport(type: Exec, dependsOn: 'jacocoFullReport') {
    group = "Reporting"
    commandLine "open", "$project.buildDir/reports/jacoco/jacocoFullReport/html/index.html"
}